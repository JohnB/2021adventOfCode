<!-- vim: syntax=markdown -->

# Day 2

## Installation and Data

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
Mix.install([
  {:kino, "~> 0.4.1"}
])

IEx.Helpers.c("lib/advent_of_code.ex")
alias AdventOfCode, as: AOC
alias Kino.Input
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
input_day = Kino.Input.number("Day")
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
input_p1example = Kino.Input.textarea("Example Data")
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
input_p1puzzleInput = Kino.Input.textarea("Puzzle Input")
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
input_source_select =
  Kino.Input.select("Source", [{:example, "example"}, {:puzzle_input, "puzzle input"}])
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
p1data = fn ->
  (Kino.Input.read(input_source_select) == :example &&
     Kino.Input.read(input_p1example)) ||
    Kino.Input.read(input_p1puzzleInput)
end
```

## Part 1

### Day 2: Dive!

Now, you need to figure out how to pilot this thing.

It seems like the submarine can take a series of commands like forward 1, down 2, or up 3:

* forward X increases the horizontal position by X units.
* down X increases the depth by X units.
* up X decreases the depth by X units.
* Note that since you're on a submarine, down and up affect your depth, and so they have the opposite result of what you might expect.

The submarine seems to already have a planned course (your puzzle input). You should probably figure out where it's going. For example:

```
forward 5
down 5
forward 8
up 3
down 8
forward 2
```

Your horizontal position and depth both start at 0. The steps above would then modify them as follows:

* forward 5 adds 5 to your horizontal position, a total of 5.
* down 5 adds 5 to your depth, resulting in a value of 5.
* forward 8 adds 8 to your horizontal position, a total of 13.
* up 3 decreases your depth by 3, resulting in a value of 2.
* down 8 adds 8 to your depth, resulting in a value of 10.
* forward 2 adds 2 to your horizontal position, a total of 15.
* After following these instructions, you would have a horizontal position of 15 and a depth of 10. (Multiplying these together produces 150.)

Calculate the horizontal position and depth you would have after following the planned course. What do you get if you multiply your final horizontal position by your final depth?

```elixir
defmodule Day2Part1 do
  def score(%{depth: depth, horizontal: horizontal}) do
    %{depth: depth, horizontal: horizontal, score: depth * horizontal}
  end

  # return a list of tuples command and delta
  def parse_commands_and_deltas(text) do
    text
    |> AdventOfCode.as_single_lines()
    |> Enum.map(fn line ->
      [command, digits] =
        line
        |> String.trim()
        |> String.split(" ")

      delta = String.to_integer(digits)

      {command, delta}
    end)
  end

  def apply_change({command, delta}, %{depth: depth, horizontal: horizontal}) do
    case command do
      "down" ->
        %{
          depth: depth + delta,
          horizontal: horizontal
        }

      "up" ->
        %{
          depth: depth - delta,
          horizontal: horizontal
        }

      "forward" ->
        %{
          depth: depth,
          horizontal: horizontal + delta
        }
    end
  end

  def solve(text) do
    text
    |> parse_commands_and_deltas()
    |> Enum.reduce(%{depth: 0, horizontal: 0}, fn {command, delta}, acc ->
      apply_change({command, delta}, acc)
    end)
    |> score()

    # %{depth: 10, horizontal: 15, score: 150}
  end
end

Day2Part1.solve(AOC.p1data())
```

## Part 2

In addition to horizontal position and depth, you'll also need to track a third value, aim, which also starts at 0. The commands also mean something entirely different than you first thought:

* down X increases your aim by X units.
* up X decreases your aim by X units.
* forward X does two things:
* It increases your horizontal position by X units.
* It increases your depth by your aim multiplied by X.
* Again note that since you're on a submarine, down and up do the opposite of what you might expect: "down" means aiming in the positive direction.

Now, the above example does something different:

* forward 5 adds 5 to your horizontal position, a total of 5. Because your aim is 0, your depth does not change.
* down 5 adds 5 to your aim, resulting in a value of 5.
* forward 8 adds 8 to your horizontal position, a total of 13. Because your aim is 5, your depth increases by 8*5=40.
* up 3 decreases your aim by 3, resulting in a value of 2.
* down 8 adds 8 to your aim, resulting in a value of 10.
* forward 2 adds 2 to your horizontal position, a total of 15. Because your aim is 10, your depth increases by 2*10=20 to a total of 60.
* After following these new instructions, you would have a horizontal position of 15 and a depth of 60. (Multiplying these produces 900.)

Using this new interpretation of the commands, calculate the horizontal position and depth you would have after following the planned course. What do you get if you multiply your final horizontal position by your final depth?

```elixir
defmodule Day2Part2 do
  def score(%{depth: depth, horizontal: horizontal, aim: aim}) do
    %{depth: depth, horizontal: horizontal, aim: aim, score: depth * horizontal}
  end

  def solve(text) do
    # NOTE: this doesn't include the code cleanup and encapsulation that was
    # applied to part 1.
    text
    |> AdventOfCode.as_single_lines()
    |> Enum.reduce(
      %{depth: 0, horizontal: 0, aim: 0},
      fn line, %{depth: depth, horizontal: horizontal, aim: aim} ->
        [command, digits] =
          line
          |> String.trim()
          |> String.split(" ")

        delta = String.to_integer(digits)

        case command do
          "down" ->
            %{
              depth: depth,
              horizontal: horizontal,
              aim: aim + delta
            }

          "up" ->
            %{
              depth: depth,
              horizontal: horizontal,
              aim: aim - delta
            }

          "forward" ->
            %{
              # increases your depth by your aim multiplied by X.
              depth: depth + aim * delta,
              horizontal: horizontal + delta,
              aim: aim
            }
        end
      end
    )
    |> score()
  end

  # %{depth: 633551, horizontal: 1975, score: 1251263225}
end

Day2Part2.solve(AOC.p2data())
```
